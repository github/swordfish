#= require application

class @Background
  constructor: (@keypair) ->
    @app = new Application()
    @app.setKeypair(@keypair) if @keypair
    @items = new Item.Collection([], keypair: @keypair)
    @submissions = {}

  # Dispatch messages from the content script and popup
  dispatch: (request, sender, response) =>
    for message, args of request
      args.push sender
      response(@[message](args...)) if @[message]

  # Recieve private key
  key: (key) ->
    unless @app.keypair
      keypair = new Keypair(key)
      @app.setKeypair(keypair)
      @items.keypair = keypair

  submit: (params, sender) ->
    attrs =
      host: @host(sender)
      data: params
    @submissions[sender.tab.id] = attrs

  connect: (sender) ->
    id = sender.tab.id
    if @submissions[id]
      chrome.experimental.infobars.show
        tabId: id
        path: "infobar.html##{id}"
        height: 40

  save: (tab_id, attrs, sender) ->
    @items.create _.extend(@submissions[tab_id], attrs)

  isUnlocked: ->
    @app.keypair.isUnlocked()

  unlock: (passphrase) ->
    if @app.keypair.unlock(passphrase)
      @app.authenticate().done => @items.fetch()

    @app.keypair.isUnlocked()

  autofill: (sender) ->
    host = @host(sender)
    if item = @items.forHost(host)
      item.data()

  setup: ->
    chrome.extension.onMessage.addListener @dispatch

  # FIXME: extract to extension adapter for sender
  host: (sender) ->
    url = document.createElement('a')
    url.href = sender.tab.url
    url.host


Application.host = '<%= ENV['APP_HOST'] %>'

@background = new Background(Keypair.load())
@background.setup()